plugins {
  id 'java-library'
  id 'maven-publish'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(16)
    vendor = JvmVendorSpec.ADOPTOPENJDK
  }

  withJavadocJar()
  withSourcesJar()
}

test {
  useJUnitPlatform()
}

// Link references to standard Java classes to the official Java 11 documentation.
javadoc.options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

dependencies {
  compileOnly project(':merlin-framework')

  api 'org.apache.commons:commons-math3:3.6.1'

  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
  testImplementation project(':merlin-framework-junit')
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
    }
  }

  publishing {
    repositories {
      if (project.hasProperty('jplArtifactoryUsername')) {
        maven {
          name = 'jplSnapshots'
          url = "https://artifactory.jpl.nasa.gov:443/artifactory/maven-libs-snapshot-local"
          credentials {
            username jplArtifactoryUsername
            password jplArtifactoryPassword
          }
        }
      }
    }
  }
}
