plugins {
  id 'java-library'
  id 'jacoco'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
  }
}

task e2eTest(type: Test) {
  ext.parseEnvFile = { filePath ->
    file(filePath).readLines().each() {
      if (!it.isEmpty() && !it.startsWith("#")) {
        def (key, value) = it.tokenize('=')
        if (key.startsWith("export ")) {
          key = key.split("export ")[1];
        }
        if (System.getenv(key) == null) {
          environment key, value
        }
      }
    }
  }

  if(file('../e2e-tests/.env').exists()) {
    parseEnvFile('../e2e-tests/.env')
  } else if(file('../.env').exists()){
    parseEnvFile('../.env')
  }

  useJUnitPlatform()
}

dependencies {
  testImplementation 'org.postgresql:postgresql:42.6.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
  testImplementation 'com.zaxxer:HikariCP:5.0.1'
}
