plugins {
  id 'java-library'
  id 'maven-publish'
  id 'jacoco'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(16)
    vendor = JvmVendorSpec.ADOPTOPENJDK
  }

  withJavadocJar()
  withSourcesJar()
}

tasks.withType(JavaCompile) {
  options.release.set(11)
}

jacocoTestReport.dependsOn test

// Link references to standard Java classes to the official Java 11 documentation.
javadoc.options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
javadoc.options.links 'https://commons.apache.org/proper/commons-lang/javadocs/api-3.9/'
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

dependencies {
  api project(':merlin-sdk')
  api 'org.glassfish:javax.json:1.1.4'

  testImplementation 'junit:junit:4.12'
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
    }
  }

  publishing {
    repositories {
      if (project.hasProperty('jplArtifactoryUsername')) {
        maven {
          name = 'jplSnapshots'
          url = "https://artifactory.jpl.nasa.gov:443/artifactory/maven-libs-snapshot-local"
          credentials {
            username jplArtifactoryUsername
            password jplArtifactoryPassword
          }
        }
      }
    }
  }
}

