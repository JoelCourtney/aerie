plugins {
  id 'java-library'
  id 'maven-publish'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(16)
    vendor = JvmVendorSpec.ADOPTOPENJDK
  }

  withJavadocJar()
  withSourcesJar()
}

tasks.withType(JavaCompile) {
  options.release.set(11)
}

// Link references to standard Java classes to the official Java 11 documentation.
javadoc.options.links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
javadoc.options.links 'https://commons.apache.org/proper/commons-lang/javadocs/api-3.9/'
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

dependencies {
  api project(':merlin-driver')
  api project(':merlin-framework')
  api 'org.junit.jupiter:junit-jupiter-api:5.7.1'
}

publishing {
  publications {
    library(MavenPublication) {
      from components.java
    }
  }

  publishing {
    repositories {
      if (project.hasProperty('jplArtifactoryUsername')) {
        maven {
          name = 'jplSnapshots'
          url = "https://artifactory.jpl.nasa.gov:443/artifactory/maven-libs-snapshot-local"
          credentials {
            username jplArtifactoryUsername
            password jplArtifactoryPassword
          }
        }
      }
    }
  }
}
