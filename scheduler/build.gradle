plugins {
  id 'java-library'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
    vendor = JvmVendorSpec.ADOPTOPENJDK
  }
}

def nameMerlinSightJar = "merlinsight-rules.jar"
def testResourceDir = "src/test/resources"
task merlinsightjar(type: Jar, dependsOn: compileTestJava)  {
    from sourceSets.test.output
    include 'gov/nasa/jpl/aerie/scheduler/MerlInsightRules.class'
    archiveFileName = nameMerlinSightJar
}

task copyJarToRes(type: Copy, dependsOn: merlinsightjar) {
    from merlinsightjar
    into testResourceDir
}

test.dependsOn copyJarToRes,merlinsightjar

clean.doFirst {
  delete testResourceDir+"/"+nameMerlinSightJar
}

test {
  useJUnitPlatform()
}

dependencies {
  api 'com.apollographql.apollo:apollo-api:2.5.11'
  compileOnly project(':merlin-framework')
  compileOnly project(':merlin-driver')
  compileOnly project(':constraints')
  compileOnly project(':contrib')
  compileOnly project(':banananation')

  implementation 'com.google.guava:guava:31.0.1-jre'
  implementation 'org.json:json:20211205'
  implementation 'org.jfree:jfreechart:1.5.3'
  implementation 'com.google.code.gson:gson:2.8.9'
  implementation 'it.univr.di:CSTNU:3.6'
  implementation 'org.jgrapht:jgrapht-core:1.5.1'
  implementation 'org.jheaps:jheaps:0.14'
  implementation 'com.squareup.okio:okio:3.0.0'
  implementation 'com.squareup.okhttp3:okhttp:4.9.0'

  testImplementation project(':merlin-framework-junit')
  testImplementation project(':constraints')
  testImplementation project(':contrib')
  testImplementation project(':banananation')
  testImplementation project(':insight')
  testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
  testImplementation 'com.google.guava:guava-testlib:31.0.1-jre'
  testImplementation 'com.google.truth.extensions:truth-java8-extension:1.1.3'
}
